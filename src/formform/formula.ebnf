(* formula notation for FORM logic
   -- created 11/2022, (c) Peter Hofmann *)

EXPR      = [LITERAL] CTX;
<CTX>     = NODE [LITERAL] CTX
          | <#'\s+'> LITERAL CTX
          | Epsilon;
<NODE>    = FORM | SEQRE | UNCLEAR | FDNA_SPEC | VAR_QUOT;
<LITERAL> = VAR | FDNA;
FORM      = <'('> [LITERAL] CTX <')'>;
TERM      = [LITERAL] CTX;
SEQRE     = <'{'> [RE_SIGN | RE_OPTS <'|'>] TERM {<','> TERM} <'}'>;
RE_SIGN   = #'@~?_?' | #'\.\.@~?\.?_?';
RE_OPTS   = RE_OPT {<'|'> RE_OPT};
FDNA_SPEC = <'['> VARLIST (CONSTS | DIGITS) <']'>;
FDNA      = CONSTS | DIGITS;
<CONSTS>  = #':[NUIM]+';
<DIGITS>  = #':[0123]+';
UNCLEAR   = <'/'> TEXT <'/'>;
VARLIST   = <'['> [(VAR_QUOT | VAR) {<','> (VAR_QUOT | VAR)}] <']'>;
VAR_QUOT  = <'"'> TEXT <'"'> | <"'"> TEXT <"'">;
VAR       = #'[a-zA-Z]\w*';
<RE_OPT>  = #'2r(\+1)?' | 'alt' | 'open';
<TEXT>    = #'[^\(\)\[\]\{\}\,\.\/‚Åò;\`\"\']+';
